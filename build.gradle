buildscript {
	repositories {
		maven { url 'https://maven.minecraftforge.net' }
		maven { url 'https://maven.parchmentmc.org' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forge_gradle_version}", changing: true
		classpath "org.parchmentmc:librarian:${librarian_version}"
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = "${mod_base_package}.${mod_id}"
archivesBaseName = "${mod_id}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
	mappings channel: "${mappings_channel}", version: "${mappings_version}"

	runs {
		data {
			taskName 'Data'
			workingDirectory file('run/client')
			// property 'forge.logging.markers', 'REGISTRIES'
			// property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			args '--mod', mod_id, '--client', '--server', '--validate', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
			// ForgeGradle will just force-exit the Gradle Daemon which fails our builds in case
			// a daemon is used for any reason.
			forceExit false

			mods {
				"${mod_id}" {
					source sourceSets.main
					source sourceSets.test
				}
			}
		}
	}
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
}

def resourceTargets = [ 'META-INF/mods.toml', '**/*.mcmeta' ]
def intoTargets = [
		"${projectDir}/out/production/resources/",
		"${projectDir}/out/production/${project.name}.main/",
		"${projectDir}/out/production/${project.name}.test/",
		"${projectDir}/bin/main/",
		"${projectDir}/bin/test/",
		"${projectDir}/build/resources/test/"
]

def replaceProperties = project.properties

processResources {
	filesMatching(resourceTargets) {
		expand replaceProperties
	}

	intoTargets.each { target ->
		if(file(target).exists()) {
			copy{
				from(sourceSets.main.resources) {
					include resourceTargets
					expand replaceProperties
				}

				into target
			}
		}
	}
}

processTestResources {
	filesMatching(resourceTargets) {
		expand replaceProperties
	}

	intoTargets.each { target ->
		if(file(target).exists()) {
			copy{
				from(sourceSets.main.resources) {
					include resourceTargets
					expand replaceProperties
				}

				into target
			}
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allJava
	from sourceSets.main.resources
}

task deobfJar(type: Jar) {
	classifier 'deobf'
	exclude '**/*bbmodel'
	from sourceSets.main.output
}

jar {
	exclude '**/*bbmodel'
	from sourceSets.main.output.classesDirs
	from sourceSets.main.output.resourcesDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives deobfJar
}

tasks.withType(Jar) {
	from file('LICENSE')

	manifest {
		attributes([
				'Specification-Title': "${mod_name}",
				'Specification-Vendor': "${mod_authors}",
				'Specification-Version': "${mod_version}",
				'Implementation-Title': "${mod_name}",
				'Implementation-Version': "${project.version}",
				'Implementation-Vendor' : "${mod_authors}",
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				'FMLModType': 'GAMELIBRARY'
		])
	}
}

jar.finalizedBy('reobfJar')

if(System.getenv('APEX_MODS_MAVEN_USERNAME') != null && System.getenv('APEX_MODS_MAVEN_PASSWORD') != null) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"

				artifact jar
				artifact sourcesJar
				artifact deobfJar
			}
		}
		repositories {
			maven {
				name 'ApexMods-Maven'
				url 'https://apexmodder.jfrog.io/artifactory/mods-maven/'

				credentials {
					username System.getenv('APEX_MODS_MAVEN_USERNAME')
					password System.getenv('APEX_MODS_MAVEN_PASSWORD')
				}
			}
		}
	}
} else {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = "${mod_base_package}"
				artifactId = "${mod_id}"
				version = "${minecraft_version}-${mod_version}"
				artifact jar
				artifact sourcesJar
				artifact deobfJar
			}
		}
		repositories {
			mavenLocal()
		}
	}
}